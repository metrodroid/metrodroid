allprojects {
    repositories {
        google()
        jcenter()
    }
}

buildscript {
    ext {
        kotlin_version = '1.2.71'
        lint_version = '26.2.1'
        support_version = '27.1.1'
    }
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'
apply plugin: 'checkstyle'
apply plugin: 'kotlin-android-extensions'

/*
check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    include '** /*.java'
    exclude '** /gen/**'
    classpath = files()
}
*/

dependencies {
    implementation ("com.android.support:appcompat-v7:$support_version") {
        exclude module: 'animated-vector-drawable'
        exclude module: 'support-fragment'
    }
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support:support-core-ui:$support_version"
    implementation ("com.android.support:support-core-utils:$support_version") {
        exclude module: 'support-compat'
    }
    implementation "com.android.support:support-vector-drawable:$support_version"
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
    implementation 'com.neovisionaries:nv-i18n:1.23'
    implementation('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    implementation project(':leaflet')
    implementation project(':material-design-icons')
    implementation project(':nfc-felica-lib')
    androidTestImplementation 'org.hamcrest:hamcrest-core:1.3'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    implementation 'com.github.bmelnychuk:atv:1.2.9'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    lintChecks project(':lintchecks')
}

protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

task copyThirdPartyNotices(type: Copy) {
    from 'third_party/'
    into 'build/assets/third_party'
    eachFile { it.path = name }
    include 'NOTICE.**.txt'
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        testApplicationId "au.id.micolous.farebot.test"
        versionCode "git -C ${projectDir} rev-list --count HEAD".execute().text.trim().toInteger()
        versionName "git -C ${projectDir} describe --always --tags --dirty".execute().text.trim().replaceAll("^v", "")

        resConfigs ([
                // Update this whenever new language translations become available in Weblate.
                // Languages we support
                'en', 'en-rAU', 'en-rCA', 'en-rGB', 'en-rIE', 'en-rIN',  'en-rNZ', 'en-rUS',
                'fi',
                'fr',
                'ga',
                // Hebrew
                'he', 'iw',
                // Indonesian
                'id', 'in',
                'ja',
                'kab',
                'nb-rNO',
                'nl',
                'ru',
                'tr',
                'zh-rCN',
                'zh-rHK',
                'zh-rSG',
                'zh-rTW',

                // UI mode


        ])
        vectorDrawables {
            useSupportLibrary = true
            generatedDensities 'ldpi', 'mdpi', 'hdpi'
        }
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
        }
        dev.initWith(buildTypes.debug)
        dev {
            applicationIdSuffix '.dev'
            minifyEnabled true
            shrinkResources true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard.cfg'
            matchingFallbacks = ['debug']
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard.cfg'
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("debug")) {
            variant.mergedFlavor.resourceConfigurations.add("en-rXA")
            variant.mergedFlavor.resourceConfigurations.add("ar-rXB")
        }
    }

    preBuild.dependsOn copyThirdPartyNotices

    sourceSets {
        androidTest.assets.srcDirs += [
                'third_party/vpereira_bilhete/',
        ]
        main.assets.srcDirs += ['build/assets']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
}
androidExtensions {
    // Needed for kotlinx.android.parcel.Parcelize
    experimental = true
}
repositories {
    mavenCentral()
}
